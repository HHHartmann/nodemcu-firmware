#!/usr/bin/env expect

# Push a file to the device, run it, and watch the tests run
#
# A typical invocation looks like:
#   TCLLIBPATH=./expectnmcu ./tap-driver.expect -serial /dev/ttyUSB3 ./mispec.lua ./mispec_file.lua
#
# For debugging the driver itself, it may be useful to invoke expect with -d,
# which will give a great deal of diagnostic information about the expect state
# machine's internals:
#
#   TCLLIBPATH=./expectnmcu expect -d ./tap-driver.expect ...
#
# The -debug option will turn on some additional reporting from this driver program, as well.


package require expectnmcu::core
package require expectnmcu::xfer

package require cmdline
set cmd_parameters {
  { serial.arg "/dev/ttyUSB0"   "Set the serial interface name" }
  { tpfx.arg   "TAP: "          "Set the expected TAP test prefix" }
  { lfs.arg    ""               "Flash a file to LFS" }
  { noxfer                      "Do not send files, just run script" }
  { debug                       "Enable debugging reporting" }
}
set cmd_usage "- A NodeMCU Lua-based-test runner"
if {[catch {array set cmdopts [cmdline::getoptions ::argv $cmd_parameters $cmd_usage]}]} {
  send_user [cmdline::usage $cmd_parameters $cmd_usage]
  send_user "\n  Additional arguments should be files be transferred\n"
  send_user "  The last file transferred will be run with `dofile`\n"
  exit 0
}

if { ${cmdopts(noxfer)} } {
  if { [ llength ${::argv} ] > 1 } {
    send_user "No point in more than one argument if noxfer given\n"
    exit 1
  }
} {
  foreach arg ${::argv} {
    if { ! [file exists ${arg}] } {
      send_user "File ${arg} does not exist\n"
      exit 1
    }
  }
}

if { ${cmdopts(lfs)} ne "" } {
  if { ! [file exists ${cmdopts(lfs)}] } {
    send_user "LFS file does not exist\n"
    exit 1
  }
}

proc sus { what } { send_user "\n===> ${what} <===\n" }
proc sui { what } { send_user "\n---> ${what} <---\n" }
proc sud { what } {
  upvar 1 cmdopts cmdopts
  if { ${cmdopts(debug)} } { send_user "\n~~~> ${what} <~~~\n" }
}

set victim [::expectnmcu::core::connect ${cmdopts(serial)}]
sus "Machine has booted"

if { ${cmdopts(lfs)} ne "" } {
  ::expectnmcu::xfer::sendfile ${victim} ${cmdopts(lfs)} "tap-driver.lfs"
  send -i ${victim} "node.flashreload(\"tap-driver.lfs\")\n"
  expect {
    -i ${victim} -ex ${::expectnmcu::core::promptstr} { }
  }
  expect {
    -i ${victim} -ex ${::expectnmcu::core::promptstr} { }
  }
}

if { ! ${cmdopts(noxfer)} } {
  foreach arg ${::argv} {
    ::expectnmcu::xfer::sendfile ${victim} ${arg} [file tail ${arg}]
  }
}

set tfn [file tail [lindex ${::argv} end ] ]

if { ${tfn} eq "" } {
  sus "No tests requested, and so operations are completed"
  exit 0
}

sus "Files transferred; running ${tfn}"

send -i ${victim} "dofile(\"${tfn}\")\n"
expect -i ${victim} -re "dofile\\(\"${tfn}\"\\)\[\r\n\]+" { }

set tpfx ${cmdopts(tpfx)}
set toeol ".*(?=\n)"

# Wait for the test to start and tell us how many
# success lines we should expect
set ntests 5
set timeout 10
expect {
  -i ${victim} -re "${tpfx}1\\.\\.(\\d+)\r?\n" {
    global ntests
    set ntests $expect_out(1,string)
  }
  -i ${victim} -re "${tpfx}Bail out!${toeol}" {
    sus "Bail out before start"
    exit 2
  }
  -i ${victim} -re ${::expectnmcu::core::panicre} {
    sus "Panic!"
    exit 2
  }
  -i ${victim} -ex "\n> " {
    sus "Prompt before start!"
    exit 2
  }
  # Consume other outputs and discard as if they were comments
  # This must come as the last pattern that looks at input
  -i ${victim} -re "(?p).${toeol}" { exp_continue }
  timeout {
    send_user "Failure: time out getting started\n"
    exit 2
  }
}

# Gobble prompt from dofile now that TAP has reported in
::expectnmcu::core::exp_prompt ${victim}

sus "Expecting ${ntests} test results"

set timeout 60
set exitwith 0
set failures 0
for {set this 0} {${this} < ${ntests}} {incr this} {
  expect {
    -i ${victim} -re "${tpfx}#${toeol}" {
      sud "Harness got comment: ${expect_out(buffer)}"
      exp_continue
    }
    -i ${victim} -re "${tpfx}ok (\\d+)\\D${toeol}" {
      sud "Harness acknowledge OK! ${this} ${expect_out(1,string)}"
      set tid ${expect_out(1,string)}
      if { ${tid} != [expr ${this} + 1 ]} {
        sui "WARNING: Test reporting misaligned at ${this}"
      }
    }
    -i ${victim} -re "${tpfx}not ok (\\d+)\\D${toeol}" {
      sud "Failure in simulation after ${this} ${expect_out(1,string)}"
      set tid ${expect_out(1,string)}
      if { ${tid} != [expr ${this} + 1 ]} {
        sui "WARNING: Test reporting misaligned at ${this}"
      }
      set exitwith [expr max(${exitwith},1)]
      set failures [expr ${failures} + 1]
    }
    -i ${victim} -re "${tpfx}Bail out!${toeol}" {
      sus "Bail out after ${this} tests"
      exit 2
    }
    # -i ${victim} -ex ${::expectnmcu::core::promptstr} {
    #   sus "Prompt while running tests!"
    #   exit 2
    # }
    -i ${victim} -re ${::expectnmcu::core::panicre} {
      sus "Panic!"
      exit 2
    }
    # Consume other outputs and discard as if they were comments
    # This must come as the last pattern that looks at input
    -re "(?p).${toeol}" { exp_continue }
    timeout {
      send_user "Failure: time out\n"
      exit 2
    }
  }
}

# We think we're done running tests; await prompt and send a final command
send -i ${victim} "print(\"f\",\"i\",\"n\")\n"
expect -i ${victim} -re "print\\(\"f\",\"i\",\"n\"\\)\[\r\n\]+" { }
expect {
  -i ${victim} -ex "f\ti\tn" { }

  -i ${victim} -re "${tpfx}#${toeol}" {
    sud "Harness got comment: ${expect_out(buffer)}"
    exp_continue
  }

  -i ${victim} -re "${tpfx}Bail out!${toeol}" {
    sus "Bail out after all tests finished"
    exit 2
  }
  -i ${victim} -re "${tpfx}${toeol}" {
    sus "Unexpected TAP output after tests finished"
    exit 2
  }
  -i ${victim} -re ${::expectnmcu::core::panicre} {
    sus "Panic!"
    exit 2
  }

  -re "(?p).${toeol}" { exp_continue }
  timeout {
    send_user "Failure: time out\n"
    exit 2
  }
}

if { ${exitwith} == 0 } {
  sus "All tests reported in OK"
} else {
  sus "${failures} TEST FAILURES; REVIEW LOGS"
}
exit ${exitwith}
